<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
               xmlns:s="library://ns.adobe.com/flex/spark"
               xmlns:mx="library://ns.adobe.com/flex/mx" initialize="setup();">
    <s:layout>
        <s:VerticalLayout/>
    </s:layout>

    <s:states>
        <s:State name="record"/>
        <s:State name="error"/>
    </s:states>
    <fx:Script><![CDATA[
        import mx.core.FlexGlobals;

        [Bindable]
        public var ref:String;

        [Bindable]
        public var record_url:String;

        [Bindable]
        public var stream_name:String;

        var nc:NetConnection;
        var ns:NetStream;

        var cam:Camera;
        var mic:Microphone;

        var cameraReady:Boolean;
        var netReady:Boolean;

        function setup():void {
            record_url = FlexGlobals.topLevelApplication.parameters.record_url;
            stream_name = FlexGlobals.topLevelApplication.parameters.stream_name;
            ref = FlexGlobals.topLevelApplication.parameters.ref;

            currentState = "record";

            ExternalInterface.addCallback("record", record);
            ExternalInterface.addCallback("stop", stop);
            ExternalInterface.addCallback("micLevel", micLevel);

            cameraReady = false;
            netReady = false;

            nc = new NetConnection();
            nc.addEventListener(NetStatusEvent.NET_STATUS, onNetStatus);
            nc.connect(record_url);

            debugMessage("Setting up flash vars and adding external interfaces");
        }

        function micLevel() {
            return mic.activityLevel;
        }

        function voidFunc(event:SampleDataEvent) {

        }

        function attachCamera():void {
            mic = Microphone.getMicrophone();

            mic.gain = 60;
            mic.rate = 11;
            mic.setUseEchoSuppression(true);
            mic.setSilenceLevel(0, -1);

            mic.addEventListener(SampleDataEvent.SAMPLE_DATA, voidFunc);

            cam = Camera.getCamera();
            cam.setMode(480, 360, 30, false);
            cam.setQuality(0, 90);
            cam.setKeyFrameInterval(30);

            cam.addEventListener(ActivityEvent.ACTIVITY, camera_activity);

            cameraDisplay.attachCamera(cam);

            debugMessage("Camera is setup, YAY");
        }

        function camera_activity(evt:ActivityEvent):void {
            if (!cameraReady) {
                debugMessage("Camera should be ready now");
                cameraReady = true;
                if (netReady) {
                    ExternalInterface.call("__video_recorder_ready", ref);
                }
            }
        }

        function record():void {
            publishCamera();
            debugMessage("Connecting to record url");

        }

        function stop():void {
            try {
                nc.close();
                ns.dispose();
            } catch (e) {
                debugMessage(e);
            }
            debugMessage("disposed : " + new Date().getTime());
            mic.addEventListener(SampleDataEvent.SAMPLE_DATA, voidFunc);
        }

        function onNetStatus(event:NetStatusEvent):void {
            debugMessage("Net Status: " + event.info.code);
            if (event.info.code == "NetConnection.Connect.Success") {
                netReady = true;
                if (cameraReady) {
                    ExternalInterface.call("__video_recorder_ready", ref);
                }
            }
        }

        function publishCamera():void {
            mic.removeEventListener(SampleDataEvent.SAMPLE_DATA, voidFunc);

            ns = new NetStream(nc);
            ns.attachCamera(cam);
            ns.attachAudio(mic);
            ns.publish(stream_name, "live");
            ExternalInterface.call("__video_recorder_recording", ref);
        }

        function showError(message:String) {
            currentState = "error";
            errorLabel.text = message;
        }

        private function debugMessage(msg:String):void {
            ExternalInterface.call("__video_recorder_debug", ref, msg);
        }


        ]]></fx:Script>

    <mx:VideoDisplay id="cameraDisplay"
                     creationComplete="attachCamera();"
                     width="100%"
                     height="100%" includeIn="record" />

    <mx:Label id="errorLabel" includeIn="error"/>


</s:Application>
